options { 
    IGNORE_CASE = true;
    NODE_DEFAULT_VOID = true;
    DEBUG_TOKEN_MANAGER = false;
    MULTI = true;
    NODE_PREFIX = "AST";
    VISITOR = true;
}

PARSER_BEGIN(BackEnd)
    public class BackEnd {
        public static SymbolTable ST = new SymbolTable();
        
        public static void main(String[] args){
            BackEnd parser;
            if (args.length == 1) {
                System.out.println("BackEnd Parser: Reading from file " + args[0] + " . . .");
                try {
                    parser = new BackEnd(new java.io.FileInputStream(args[0]));
                } catch (java.io.FileNotFoundException e) {
                    System.out.println("BackEnd Parser: File " + args[0] + " not found.");
                    return;
                }
            } else {
                    System.out.println("BackEnd Parser: Usage is one of:");
                    System.out.println(" java BackEndParser < inputfile");
                    System.out.println("OR");
                    System.out.println(" java BackEndParser inputfile");
                    return;
            } 
            try {
                SimpleNode root = parser.Program();
                root.dump("");
                SCVisitor pv = new SCVisitor();
                root.jjtAccept(pv, "");
                System.out.println("CCAL Parser: CCAL program parsed successfully.");
                } catch (ParseException e) {
                System.out.println(e.getMessage());
                System.out.println("CCAL Parser: Encountered errors during parse.");
            }
        }
    }
PARSER_END(BackEnd)

/*Token*/

TOKEN: { /* Keywords */
    < VAR : "var" >
    | < CONST : "const" >
    | < RETURN : "return" >
    | < BOOLEAN : "boolean" >
    | < VOID : "void" >
    | < MAIN : "main" >
    | < IF : "if" >
    | < ELSE : "else" >
    | < TRUE : "true" >
    | < FALSE : "false" >
    | < WHILE : "while" >
    | < SKP : "skip" >
    | < INTEGER : "integer">
}

TOKEN: {    /* Punctuation */ 
    < COMMA : "," >
    | < SEMIC : ";" >
    | < COLON : ":" >
    | < ASSIGN : "=" >
    | < LBRC : "{" >
    | < RBRC : "}" >
    | < LPAR : "(" >
    | < RPAR : ")" >
    | < PLUS_SIGN : "+" >
    | < MINUS_SIGN : "-" >
    | < TILDE : "~" >
    | < OR : "||" >
    | < AND : "&&" >
    | < EQUIV : "==" >
    | < NOTEQUIV : "!=" >
    | < LTHAN : "<" >
    | < GTHAN : ">" >
    | < LETHAN : "<=" >
    | < GETHAN : ">=" >
}

TOKEN: { /* Numbers and identifiers */
      < #DIGIT : ["0"-"9"] >
    | < #CHAR : ["a"-"z", "A"-"Z"] >
    | < NUMBER : ("-")* ["1"-"9"] (<DIGIT>)* | "0" >
    | < ID : <CHAR> (<CHAR>| "_" | <DIGIT>)* >
}

TOKEN_MGR_DECLS :
{
    static int commentNesting = 0;
}

SKIP : { /*Ignore whitespace*/
     " "
    | "\t"
    | "\n"
    | "\f"
    | "\r"
}

SKIP : {
    < "//" ([" " - "~"])* ("\n" | "\r" | "\r\n") >
    | "/*" { commentNesting++; } : IN_COMMENT
}

<IN_COMMENT> SKIP : {
      "/*" {commentNesting++; }
    | "*/" {commentNesting--; 
            if (commentNesting == 0)
                SwitchTo(DEFAULT);
            }
    | <~[]>
}

SimpleNode Program() #Root: {}
{
    DeclList() FunctionList() Main() <EOF> {return jjtThis;}
}

void DeclList(): {}
{
    Decl() <SEMIC> DeclList() 
    | {}
}

void Decl(): {}
{
    VarDecl()
    | ConstDecl()
}

void VarDecl() #Var: {Token t; String id; String type;}
{
    t = <VAR> id = LId() <COLON> type = Type()
}

void ConstDecl() #Const: {Token t; String id; String type;}
{
    t = <CONST> id = LId() <COLON> type = Type() <ASSIGN> Expression() 
}

void FunctionList(): {}
{
    Function() #FBlock FunctionList()
    | {}
}

void Function() #Func: {Token t; String type; String id;}
{
    type = Type() id = LId() <LPAR> ParameterList() <RPAR> 
    <LBRC>
    DeclList() StatementBlock() 
    t = <RETURN> <LPAR> (Expression() #Return | {}) <RPAR> <SEMIC> 
    <RBRC>
}


void ParameterList(): {Token t;}
{
    NempParameterList() #ParamList()
    | {}
}

void NempParameterList() #Param: {Token t;}
{
    Id() <COLON> Type() (<COMMA> NempParameterList() | {})
}

void Main() #Main: {Token t;}
{
    <MAIN> <LBRC>
    DeclList() StatementBlock() 
    <RBRC>
}

void StatementBlock(): {}
{
    Statement() #SBlock StatementBlock()
    | {}
}

void Statement(): {Token t;}
{
    LId() (t = <ASSIGN> Expression() <SEMIC> {jjtThis.value = t.image;} #Assign(2) | FuncCall() <SEMIC>)
    | <LBRC> StatementBlock() <RBRC>
    | t = <IF> Condition() <LBRC> StatementBlock() <RBRC> 
        <ELSE> <LBRC> StatementBlock() <RBRC> {jjtThis.value = t.image;} #IfBlock(3)
    | t = <WHILE> Condition() <LBRC> StatementBlock() <RBRC> #While(2)
    | t = <SKP> <SEMIC> #Skip
}

void Expression(): {}
{
     Fragment() ExpressionPrime()
    | <LPAR> Expression() <RPAR> ExpressionPrime()
}

void ExpressionPrime(): {}
{
    BinaryArithOp() ExpressionPrime()
    | {}
}

void BinaryArithOp() : {Token t;} 
{
    t=<PLUS_SIGN> Expression() #Plus(2)
    | t=<MINUS_SIGN> Expression() #Minus(2)
}
void FuncCall() : {}
{
    <LPAR> ArgList() <RPAR> #FuncArgs(2)
    | {}
}

void Fragment() : {Token t;}
{
    t = <MINUS_SIGN> Id() {jjtThis.value = t.image;} #Neg(1) 
    | Id() FuncCall()
    | Num() 
    | t = <TRUE> {jjtThis.value = t.image;} #True
    | t = <FALSE> {jjtThis.value = t.image;} #False
}

void Condition(): {}
{ 
    <TILDE> Condition() ConditionPrime()
    | <LPAR> Condition() <RPAR> ConditionPrime()
    | Fragment() (CompOp() | {}) ConditionPrime()
}

void ConditionPrime(): {Token t;}
{
    t = <OR> Condition() ConditionPrime() {jjtThis.value = t.image;} #OR(2)
    | t = <AND> Condition() ConditionPrime() {jjtThis.value = t.image;} #AND(2)
    | {}
}

void CompOp(): {Token t;}
{
    t = <EQUIV> Expression() #Equiv(2)
    | t = <NOTEQUIV> Expression() #NotEquiv(2)
    | t = <LTHAN> Expression() #LThan(2)
    | t = <LETHAN> Expression() #LEThan(2)
    | t = <GTHAN> Expression() #GTHAN(2)
    | t = <GETHAN> Expression() #GeThan(2)
}

void ArgList(): {}
{
    NempArgList() #ArgList
    | {}
}

void NempArgList(): {}
{
    Id() (<COMMA> NempArgList() | {})
}

String Type() #Type: {Token t;}
{
    t = <INTEGER> {jjtThis.value = t.image; return t.image;}
    | t = <BOOLEAN> {jjtThis.value = t.image; return t.image;}
    | t = <VOID> {jjtThis.value = t.image; return t.image;}
}

String LId() #LID: {Token t;}
{
    t = <ID> {jjtThis.value = t.image; return t.image;}
}

String Id() #ID: {Token t;}
{
    t = <ID> {jjtThis.value = t.image; return t.image;}
}

String Num() #Num: {Token t;}
{
    t = <NUMBER> {jjtThis.value = t.image; return t.image;}
}